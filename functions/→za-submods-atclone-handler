# -*- mode: zsh; sh-indentation: 2; indent-tabs-mode: nil; sh-basic-offset: 2; -*-
# vim: ft=zsh sw=2 ts=2 et

emulate -L zsh -o extendedglob -o warncreateglobal -o typesetsilent
[[ -z "${ICE[submods]}" ]] && return 0

[[ "$1" = plugin ]] && \
local type="$1" user="$2" plugin="$3" id_as="$4" dir="$5" || local type="$1" url="$2" id_as="$3" dir="$4"
local -a mods parts
local mod

mods=( ${(@s.;.)ICE[submods]} )
for mod in "${mods[@]}"; do
  parts=( "${(@s:->:)mod}" )
  # Remove only leading and trailing whitespace
  parts=( "${parts[@]//((#s)[[:space:]]##|[[:space:]]##(#e))/}" )
  
  +zi-message "{pre}z-a-submods{ehi}: {bar}@@{rst} Cloning submodule {bar}@@{ehi}: {pname}${parts[1]}{rst}" \
  "to {info}${parts[2]}{rst}"
  
  if [[ "${parts[1]}" != (http|https|ftp|ftps|scp)://* ]]; then
    parts[1]="https://github.com/${parts[1]}"
  fi
  command git -C "$dir" clone --progress "${parts[1]}" "${parts[2]}" |& ${ZI[BIN_DIR]}/lib/zsh/git-process-output.zsh
done
